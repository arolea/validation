import com.rolea.learning.validation.domain.Product;
import com.rolea.learning.validation.domain.Category;
import com.rolea.learning.validation.service.ValidationWarning;
import com.rolea.learning.validation.service.ValidationResultContainer;
import com.rolea.learning.validation.service.ProductAdditionalData;

global ValidationResultContainer resultContainer;
global ProductAdditionalData additionalData;

function boolean validateClashingCategories(Product product) {
    String firstCategoryName = "Category 1";
    String secondCategoryName = "Category 2";

    if(product.getCategories() == null || product.getCategories().size() == 0){
        return false;
    }

    boolean containsFirstCategory = product.getCategories().stream()
        .anyMatch(category -> firstCategoryName.equals(category.getName()));
    boolean containsSecondCategory = product.getCategories().stream()
            .anyMatch(category -> secondCategoryName.equals(category.getName()));

    return containsFirstCategory && containsSecondCategory;
}

rule "Product has no clashing categories (a product can't be part of both 'Category 1' and 'Category 2')"
    salience 10
    when
        productInstance:Product()
        eval(validateClashingCategories(productInstance))
    then
        String message = String.format("Product with id %d has clashing categories", productInstance.getId());
        resultContainer.addValidationWarning(productInstance, message);
end

// for the when clause, you can chain conditions with 'and', 'or' or 'not'
// if none is specified, 'and' is assumed
// it is a best practice that variable defined in when start with '$'
// you can also define variables while evaluating a fact (see $categories)
// the ',' operator has the same semantics as '&&', operator precedence: '&&' > '||' > ','
rule "Product has no clashing categories (a product can't be part of both 'Category 1' and 'Category 2') - no function"
    salience 10
    when
        $productInstance : Product($categories : categories) and
        Category(name == "Category 1") from $categories and
        Category(name == "Category 2") from $categories
    then
        String message = String.format("Product with id %d has clashing categories", $productInstance.getId());
        resultContainer.addValidationWarning($productInstance, message);
end